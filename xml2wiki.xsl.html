<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

<xsl:include href="myxml.xsl" />

<xsl:output method="text" indent="no" encoding="utf-8" />

<xsl:strip-space elements="*" />
<xsl:preserve-space elements="" />

<xsl:template match="font">
  <xsl:apply-templates select="node()" />
</xsl:template>

<xsl:template match="q">
  <xsl:text>« </xsl:text>
  <xsl:value-of select="text()" />
  <xsl:text> »</xsl:text>
</xsl:template>

<xsl:template match="a">
  <xsl:if test="@href">
    <xsl:choose>
	  <xsl:when test="substring(@href,1,1)!='#'">
		  <xsl:text>[</xsl:text>
		  <xsl:value-of select="@href" />
		  <xsl:text> </xsl:text>
		  <xsl:apply-templates select="node()"/>
		  <xsl:text>]</xsl:text>
	  </xsl:when>
	  <xsl:otherwise>
		  <xsl:apply-templates select="node()"/>
	  </xsl:otherwise>
	</xsl:choose>
  </xsl:if>
</xsl:template>

<xsl:template match="ul|ol">
  <xsl:apply-templates select="* | text() | @*"/>
  <xsl:text>&#10;</xsl:text>
</xsl:template>

<xsl:template match="li">
  <xsl:choose>
    <xsl:when test="name(..) = 'ol'">
	  <xsl:number format="1. " />
    </xsl:when> 
    <xsl:otherwise> 
      <xsl:text>* </xsl:text>
    </xsl:otherwise> 
  </xsl:choose>

  <xsl:apply-templates select="* | text() | @*"/>
  <xsl:text>&#10;</xsl:text>
</xsl:template>

<xsl:template match="pre">
  <xsl:text>&lt;pre&gt;&lt;nowiki&gt;</xsl:text>
    <xsl:apply-templates select="* | text() | @*"/>
  <xsl:text>&lt;/nowiki&gt;&lt;/pre&gt;&#10;&#10;</xsl:text>
</xsl:template>

<xsl:template match="h1">
  <xsl:text>&#10;= </xsl:text>
  <xsl:apply-templates select="node()"/>
  <xsl:text> =&#10;</xsl:text>
</xsl:template>

<xsl:template match="h2">
  <xsl:text>&#10;== </xsl:text>
  <xsl:apply-templates select="node()" />
  <xsl:text> ==&#10;</xsl:text>
</xsl:template>

<xsl:template match="h3">
  <xsl:text>&#10;=== </xsl:text>
  <xsl:apply-templates select="node()" />
  <xsl:text> ===&#10;&#10;</xsl:text>
</xsl:template>

<xsl:template match="h4">
  <xsl:text>&#10;==== </xsl:text>
  <xsl:apply-templates select="node()" />
  <xsl:text> ====&#10;&#10;</xsl:text>
</xsl:template>

<xsl:template match="table">
  <xsl:text>{| border="1"</xsl:text>
  <xsl:apply-templates select="*"/>
  <xsl:text>&#10;|}&#10;&#10;</xsl:text>
</xsl:template>

<xsl:template match="tr">
  <xsl:text>&#10;|-&#10;</xsl:text>
  <xsl:apply-templates select="*"/>
</xsl:template>

<xsl:template match="th">
  <xsl:choose>
    <xsl:when test="position()=1">
      <xsl:text>! </xsl:text>
	</xsl:when>
	<xsl:otherwise>
      <xsl:text>!! </xsl:text>
	</xsl:otherwise>
  </xsl:choose>
  <xsl:apply-templates select="node()"/>
  <xsl:text> </xsl:text>
</xsl:template>

<xsl:template match="td">
  <xsl:choose>
    <xsl:when test="position()=1">
      <xsl:text>| </xsl:text>
	</xsl:when>
	<xsl:otherwise>
      <xsl:text>|| </xsl:text>
	</xsl:otherwise>
  </xsl:choose>
  <xsl:apply-templates select="node()"/>
  <xsl:text> </xsl:text>
</xsl:template>

<xsl:template match="p">
   <xsl:apply-templates select="node()" />
   <xsl:text>&#10;&#10;</xsl:text>
</xsl:template>

<xsl:template match="br">
<!--  <xsl:text>&#10;</xsl:text> -->
  <xsl:text>&lt;br /&gt;</xsl:text>
</xsl:template>

<xsl:template match="@*">
</xsl:template>

<xsl:template match="text()">
  <xsl:choose>
    <xsl:when test="name(..) = 'pre'">
      <xsl:value-of select="." />
    </xsl:when> 
    <xsl:otherwise> 
      <xsl:value-of select="normalize-space(.)" />
    </xsl:otherwise> 
  </xsl:choose>
</xsl:template>

<xsl:template match="*">
  <xsl:element name="{name()}" >
    <xsl:apply-templates select="@* | node()" />
  </xsl:element>
</xsl:template>

</xsl:stylesheet>

